%{
#include "parser.h"
#include "core_def.h"
%}

%includes
%language=ANSI-C
%readonly-tables
%struct-type
%ignore-case
%define initializer-suffix ,{0,0}
%define slot-name name

struct _prim_op_gperf {
    const char *name;
    PrimOp op;
};

%%
ADD,     {TOK_ADD,     add     }
APPLY,   {TOK_APPLY,   apply   }
CLOSURE, {TOK_CLOSURE, closure }
DIV,     {TOK_DIV,     div_    }
EQ,      {TOK_EQ,      eq      }
FALSE,   {TOK_FALSE,   0       }
IF,      {TOK_IF,      0       }
IS,      {TOK_IS,      is      }
LOOKUP,  {TOK_LOOKUP,  lookup  }
MUL,     {TOK_MUL,     mul     }
PUSH,    {TOK_PUSH,    push    }
QUOTE,   {TOK_QUOTE,   quote   }
RETURN,  {TOK_RETURN,  return_ }
SET,     {TOK_SET,     set     }
SUB,     {TOK_SUB,     sub     }
TRUE,    {TOK_TRUE,    0       }
%%

const PrimOp * prim_op_lookup(register const char *str, register unsigned int len) {
    const struct _prim_op_gperf *_op = in_word_set(str, len);
    if (_op) {
        return &_op->op;
    } else {
        return 0;
    }
}
