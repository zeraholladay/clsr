%{
#include "parser.h"
#include "prim_op.h"
%}

%includes
%language=ANSI-C
%readonly-tables
%struct-type
%define initializer-suffix ,{0,0}
%define slot-name name

struct _prim_op_gperf {
    const char *name;
    PrimOp op;
};

%%
APPLY,   {TOK_APPLY,   PrimOp_Apply   }
CLOSURE, {TOK_CLOSURE, PrimOp_Closure }
LOOKUP,  {TOK_LOOKUP,  PrimOp_Lookup  }
PUSH,    {TOK_PUSH,    PrimOp_Push    }
RETURN,  {TOK_RETURN,  PrimOp_Return  }
SET,     {TOK_SET,     PrimOp_Set     }
%%

const PrimOp * prim_op_lookup(register const char *str, register unsigned int len) {
    const struct _prim_op_gperf *_op = in_word_set(str, len);
    if (_op) {
        return &_op->op;
    } else {
        return 0;
    }
}
