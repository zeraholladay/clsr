// clang-format off
%{
// clang-format off
#include <string.h>

#include "eval.h"
#include "eval_math.h"
#include "keywords.h"
// clang-format off
%}

%includes
%readonly-tables
%global-table
%define word-array-name keyword_table
%struct-type
%ignore-case
%define lookup-function-name keyword_lookup
%define initializer-suffix ,0,0
%define slot-name name

struct Keyword;

%%
APPLY,   eval_apply,    2
CONS,    eval_cons,     2
EQ,      eval_eq,       2
EVAL,    eval,          1
FIRST,   eval_first,    1
FUNCALL, eval_funcall, -1
IF,      NULL,          0
LAMBDA,  NULL,          0
LEN,     eval_len,      1
NIL,     NULL,          0
LIST,    eval_list,    -1
PAIR,    eval_pair,     2
PRINT,   eval_print,    1
QUOTE,   NULL,          0
REST,    eval_rest,     1
SET,     eval_set,      2
STR,     eval_str,      1
T,       NULL,          0
ADD,     eval_add,     -1
SUB,     eval_sub,     -1
MUL,     eval_mul,     -1
DIV,     eval_div,     -1
%%

// clang-format off

#define PRIN_FN_TAB_NUM                                                       \
  ((int)(sizeof (keyword_table) / sizeof (keyword_table[0])))

const char *
is_keyword_strncmp (const char *text, int state)
{
  static int idx;
  static size_t len;

  if (state == 0)
    {
      idx = 0;
      len = strlen (text);
    }

  while (idx < PRIN_FN_TAB_NUM)
    {
      const char *name = keyword_table[idx++].name;
      if (!strncasecmp (name, text, len))
        {
          return name;
        }
    }
  return NULL;
}
//clang-format off
