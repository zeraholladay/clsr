%{
#include "parser.h"
#include "prim_op.h"
%}

%includes
%language=ANSI-C
%readonly-tables
%struct-type
%define initializer-suffix ,{0}
%define slot-name name

struct _prim_op_gperf {
    const char *name;
    PrimOp op;
};

%%
APPLY,   {TOK_APPLY   }
CLOSURE, {TOK_CLOSURE }
LOOKUP,  {TOK_LOOKUP  }
PUSH,    {TOK_PUSH    }
RETURN,  {TOK_RETURN  }
SET,     {TOK_SET     }
%%

const PrimOp * prim_op_lookup(register const char *str, register unsigned int len) {
    const struct _prim_op_gperf *_op = in_word_set(str, len);
    if (_op) {
        return &_op->op;
    } else {
        return 0;
    }
}
